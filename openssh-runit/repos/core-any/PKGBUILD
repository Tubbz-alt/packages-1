# Maintainer: Muhammad Herdiansyah <herdiansyah@netc.eu>

_url="https://github.com/voidlinux/void-packages/raw/master/srcpkgs"

_sed_args=(-e 's|/var/run|/run|g' -e 's|\(/usr\)\?/sbin|/usr/bin|g')

pkgname=openssh-runit
pkgver=20180429
pkgrel=1
pkgdesc="runit service scripts for openssh"
arch=('any')
url="https://github.com/artix-linux"
license=('BSD3')
groups=('runit-system')
depends=('openssh')
provides=('openssh-runscripts' 'init-openssh')
conflicts=('init-openssh')
replaces=('openssh-runscripts')
source=("sshd.run::${_url}/openssh/files/sshd/run")
sha256sums=('220dde8d4495bab6f08e063860c0ad54cc70ef8b7913a00e27126f1d5eb0af5f')

_inst_sv(){
    for file in run finish check; do
        if test -f "$srcdir/$1.$file"; then
            install -Dm755 "$srcdir/$1.$file" "$pkgdir/etc/runit/sv/$1/$file"
            sed "${_sed_args[@]}" -i "$pkgdir/etc/runit/sv/$1/$file"
        fi
    done
}

package() {
    _inst_sv 'sshd'
}
