# Mainainer: SÃ©bastien "Seblu" Luttringer <seblu@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=tar
pkgver=1.30
pkgrel=2
pkgdesc='Utility used to store, backup, and transport files'
arch=('x86_64')
url='https://www.gnu.org/software/tar/'
license=('GPL3')
groups=('base')
depends=('glibc' 'acl' 'attr')
options=('!emptydirs')
validpgpkeys=('325F650C4C2B6AD58807327A3602B07F55D0C732') # Sergey Poznyakoff
source=("https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig}
        "tar-1.30-tests-difflink.patch"
        "tar-1.30-tests-dirrem.patch")
md5sums=('2d01c6cd1387be98f57a0ec4e6e35826'
         'SKIP'
         '03a5a69df0dc6bfc8a5aa312f7535173'
         '4ebab6318576ada4b244690f7acb134c')

prepare() {
  cd $pkgname-$pkgver
  # apply patch from the source array (should be a pacman feature)
  local filename
  for filename in "${source[@]}"; do
    if [[ "$filename" =~ \.patch$ ]]; then
      msg2 "Applying patch ${filename##*/}"
      patch -p1 -N -i "$srcdir/${filename##*/}"
    fi
  done
  :
  
  autoreconf -if
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/tar
  make
}

check() {
  cd $pkgname-$pkgver
  
  # need to update testsuite script after patching
  rm tests/testsuite
  make -C tests/ testsuite

  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
