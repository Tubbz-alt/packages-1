# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=glslang
pkgver=7.10.2984
pkgrel=1
pkgdesc='OpenGL and OpenGL ES shader front end and validator'
arch=('x86_64')
url='https://github.com/KhronosGroup/glslang'
license=('BSD')
depends=('gcc-libs' 'spirv-tools')
makedepends=('cmake')
options=('staticlibs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/KhronosGroup/glslang/archive/${pkgver}.tar.gz)
sha256sums=('d0afe88034577ecf06a825db1a5e8222d949a38d3b19c5ce002b90b66c403f67')

build() {
  cd ${pkgname}-${pkgver}
  mkdir -p build-{shared,static}
  (cd build-shared
    cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release \
      -DBUILD_SHARED_LIBS=ON
    make
  )
  (cd build-static
    cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release \
      -DBUILD_SHARED_LIBS=OFF
    make
  )
}

package() {
  cd ${pkgname}-${pkgver}
  make -C build-shared DESTDIR="${pkgdir}" install
  make -C build-static DESTDIR="${pkgdir}" install
  cd "${pkgdir}"/usr/lib
  for lib in *.so; do
    ln -sf "${lib}" "${lib}.0"
  done
}

# vim: ts=2 sw=2 et:
