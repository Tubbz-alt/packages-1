# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cantarell-fonts
pkgver=0.111
pkgrel=1
epoch=1
pkgdesc="Humanist sans serif font"
url="https://gitlab.gnome.org/GNOME/cantarell-fonts"
arch=(any)
license=(custom:SIL)
makedepends=(meson appstream-glib git)
_commit=9c0eb3dc8c0b2e0d5b9e4dfcd48ca1b9531baaa9  # tags/v0.111^0
source=("git+https://gitlab.gnome.org/GNOME/cantarell-fonts.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  python -m venv venv
  venv/bin/pip install fontmake psautohint
  cd $pkgname
}

build() {
  PATH="$srcdir/venv/bin:$PATH" arch-meson $pkgname build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname/COPYING
}

# vim:set ts=2 sw=2 et:
