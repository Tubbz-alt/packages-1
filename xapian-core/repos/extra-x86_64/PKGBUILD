# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=xapian-core
epoch=1
pkgver=1.4.9
pkgrel=1
pkgdesc='Open source search engine library.'
arch=('x86_64')
url='http://www.xapian.org/'
license=('GPL')
depends=('util-linux')
# xapian config requires libxapian.la
options=('libtool')
source=("http://oligarchy.co.uk/xapian/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha512sums=('1af23815ff9358d6407723d1a7d3dc00df8f45f4808ccaa4c57f38197763fdc2d62cb7b080fab737408db42ced85707021eac881d80767248ea22aff8a2e4aa3')

build() {
  # FS#40614
  if [ "${CARCH}" = "i686" ]; then
    SSE2="--disable-sse"
  fi

  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr ${SSE2}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
