# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Charles Mauch <cmauch@gmail.com>

pkgname=perl-yaml
pkgver=1.27
pkgrel=1
pkgdesc="Perl/CPAN Module YAML : YAML Aint Markup Language"
arch=('any')
url="http://search.cpan.org/dist/YAML/"
license=('GPL' 'PerlArtistic')
depends=('perl')
checkdepends=('perl-test-deep' 'perl-test-yaml')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/T/TI/TINITA/YAML-${pkgver}.tar.gz)
sha512sums=('f82de359939f139211df1c63226a17c0ecb00833c8e72986e901507626ef19f3bb3dd7678c83abb9aeb8857ca921e7698e2c0cb0620de5e4a8d3b963ee040e66')

build() {
  cd YAML-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd YAML-${pkgver}
  make test
}

package() {
  cd YAML-${pkgver}
  make DESTDIR="${pkgdir}" install
}
