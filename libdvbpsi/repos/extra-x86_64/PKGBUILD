# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Gilles CHAUVIN <gcnweb@gmail.com>

pkgname=libdvbpsi
pkgver=1.3.2
pkgrel=1
epoch=1
pkgdesc='Library designed for decoding and generation of MPEG TS and DVB PSI tables'
url='http://developers.videolan.org/libdvbpsi/'
arch=('x86_64')
license=('LGPL2.1')
depends=('glibc')
provides=('libdvbpsi.so')
source=(${pkgname}-${pkgver}.tar.bz2::https://code.videolan.org/videolan/libdvbpsi/repository/${pkgver}/archive.tar.bz2)
sha512sums=('598a671059f8d1ffe19c695972a07bf438cc8a340d7f3d00745b9c55be767acb6848a26b9af4918aa9652db76ae32c3ffdacad860259ae2092ffb8b48203e6b3')

prepare() {
  cd ${pkgname}-${pkgver}-*
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}-*
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}-*
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
