# Maintainer: Muhammad Herdiansyah <herdiansyah@netc.eu>

_url="https://github.com/voidlinux/void-packages/raw/master/srcpkgs"

_sed_args=(-e 's|/var/run|/run|g' -e 's|\(/usr\)\?/sbin|/usr/bin|g')

pkgname=lvm2-runit
pkgver=20180514
pkgrel=5
pkgdesc="runit service and stage1 scripts for lvm2"
arch=('any')
url="https://github.com/artix-linux"
license=('BSD3')
groups=('runit-system')
depends=('lvm2' 'runit')
provides=('lvm2-runscripts' 'init-lvm2')
conflicts=('init-lvm2')
replaces=('lvm2-runscripts')
source=("lvmetad.run::${_url}/lvm2/files/lvmetad/run"
        "lvm2-monitoring"
        "lvm2")
sha256sums=('ed490eab75c0ff3e2d9f1802c47478198725e9b16cfd03392f7f03d2d65b6fc2'
            '7a1cdb90aa145a2904bac250d9069a3b7a94330f65d06141c1f89a613c0a8dad'
            'a8375e73686b0e18bda9da7e8cffd3eb073c61eb52fde79cc473ebdc458e688e')

_inst_sv(){
    for file in run finish check; do
        if test -f "$srcdir/$1.$file"; then
            install -Dm755 "$srcdir/$1.$file" "$pkgdir/etc/runit/sv/$1/$file"
            sed "${_sed_args[@]}" -i "$pkgdir/etc/runit/sv/$1/$file"
        fi
    done
}

package() {
    _inst_sv 'lvmetad'
    install -Dm755 "${srcdir}/lvm2" "${pkgdir}/usr/lib/rc/sv.d/lvm2"
    install -Dm755 "${srcdir}/lvm2-monitoring" "${pkgdir}/usr/lib/rc/sv.d/lvm2-monitoring"

    # create default symlinks?
    install -d ${pkgdir}/etc/rc/{sysinit,shutdown}
    ln -sf /usr/lib/rc/sv.d/lvm2 ${pkgdir}/etc/rc/sysinit/34-lvm2
    ln -sf /usr/lib/rc/sv.d/lvm2 ${pkgdir}/etc/rc/shutdown/64-lvm2
}
