# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=libdbusmenu-qt
pkgname=(libdbusmenu-qt5)
pkgver=0.9.3+16.04.20160218
pkgrel=2
pkgdesc="A library that provides a Qt implementation of the DBusMenu spec"
arch=(x86_64)
url="https://launchpad.net/libdbusmenu-qt"
license=(GPL)
makedepends=(cmake doxygen qjson qt5-base)
source=("http://archive.ubuntu.com/ubuntu/pool/main/libd/$pkgbase/${pkgbase}_$pkgver.orig.tar.gz")
md5sums=('8b90312bd8654e026d814d4db986ce5e')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgbase-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package_libdbusmenu-qt5() {
  depends=(qt5-base)

  cd build
  make DESTDIR="$pkgdir" install
}
