# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jochem Kossen <j.kossen@home.nl>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=aspell
_pkgver=0.60.7-rc1
pkgver=${_pkgver/-/}
_pkgmajorver=0.60
pkgrel=1
pkgdesc="A spell checker designed to eventually replace Ispell"
arch=('x86_64')
url="http://aspell.net/"
license=('LGPL')
depends=('gcc-libs' 'ncurses')
optdepends=('perl: to import old dictionaries')
source=("https://github.com/GNUAspell/aspell/archive/rel-$_pkgver.tar.gz")
sha512sums=('8ba34c2de543f82d4f638dca945a6e465e3c21ffd1b8a2f08b6c6b7b326c78d15c35607257e2e9939c61e95fc06951d06249eb5d2847a8750b592c11d4402875')

build() {
  cd $pkgname-rel-$_pkgver
  PERL_USE_UNSAFE_INC=1 ./autogen
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd $pkgname-rel-$_pkgver
  make DESTDIR="$pkgdir" install
  ln -s $pkgname-$_pkgmajorver "$pkgdir"/usr/lib/$pkgname
}
