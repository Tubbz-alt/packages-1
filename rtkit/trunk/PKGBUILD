# Maintainer: Muhammad Herdiansyah <koni@artixlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>

pkgname=rtkit
pkgver=0.11+9
_pkgver=0.11
pkgrel=1
pkgdesc="Realtime Policy and Watchdog Daemon"
arch=(x86_64)
url="http://git.0pointer.de/?p=rtkit.git"
license=(GPL3 'custom:BSD')
depends=(dbus polkit)
source=("http://0pointer.de/public/$pkgname-$_pkgver.tar.xz")
sha256sums=('68859108cff6410901502b58365eb7607da37110a06b837762f771735f58acd0')

build() {
  cd $pkgname-$_pkgver
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib

  make

  ./rtkit-daemon --introspect > org.freedesktop.RealtimeKit1.xml
}

package() {
  cd $pkgname-$_pkgver
  make DESTDIR="$pkgdir" install

  install -Dt "$pkgdir/usr/share/dbus-1/interfaces" -m644 org.freedesktop.RealtimeKit1.xml

  echo 'u rtkit 133 "RealtimeKit" /proc' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"

  sed -ne '4,25p' rtkit.c |
    install -Dm644 /dev/stdin "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENSE
}
