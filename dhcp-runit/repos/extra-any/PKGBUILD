# Maintainer: Muhammad Herdiansyah <herdiansyah@netc.eu>

_url="https://github.com/voidlinux/void-packages/raw/master/srcpkgs"

_sed_args=(-e 's|/var/service|/run/runit/service|g' -e 's|/var/run|/run|g' -e 's|/usr/sbin|/usr/bin|g' -e 's|/opt/bin|/usr/bin|g' -e 's|/usr/libexec|/usr/lib|g')

pkgname=dhcp-runit
pkgver=20180226
pkgrel=1
pkgdesc="runit service scripts for dhcp"
arch=('any')
url="https://github.com/artix-linux"
license=('BSD3')
# Note: While this PKGBUILD is licensed under BSD-3 terms, all of the
#       included runscript should follow it's main package's licenses.
groups=('runit-world')
depends=('dhcp')
provides=('dhcp-runscripts')
replaces=('dhcp-runscripts')
conflicts=('systemd-sysvcompat')
source=("dhclient.run::${_url}/dhcp/files/dhclient/run"
        "dhcpd4.run::${_url}/dhcp/files/dhcpd4/run"
        "dhcpd6.run::${_url}/dhcp/files/dhcpd6/run")
sha256sums=('f351d7a77ec4ae3e4eeb9e11fc354bd76999041a0dc4b010c28db79bb180b85e'
            'e9b97d568b8e86a74d8dc8774f61fa9c50fe273805d6080d4c192223e013e689'
            '9272cc1d3a882ed44023d0289633b601551f985fbc42831e3866daad8c2a69d5')

_inst_logsv() {
    for file in run finish check; do
        if test -f "$srcdir/log$1.$file"; then
            install -Dm755 "$srcdir/log$1.$file" "$pkgdir/etc/runit/sv/$1/log/$file"
            sed "${_sed_args[@]}" -i "$pkgdir/etc/runit/sv/$1/log/$file"
        fi
    done
}

_inst_sv() {
    if test -f "$srcdir/$1.conf"; then
        install -Dm644 "$srcdir/$1.conf" "$pkgdir/etc/runit/sv/$1/conf"
    fi

    for file in run finish check; do
        if test -f "$srcdir/$1.$file"; then
            install -Dm755 "$srcdir/$1.$file" "$pkgdir/etc/runit/sv/$1/$file"
            sed "${_sed_args[@]}" -i "$pkgdir/etc/runit/sv/$1/$file"
        fi
    done
}

package() {
    _inst_sv 'dhclient'
    _inst_sv 'dhcpd4'
    _inst_sv 'dhcpd6'
}
