# Maintainer: SÃ©bastien Luttringer <seblu@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=grep
pkgver=3.1
pkgrel=2
pkgdesc='A string search utility'
arch=('x86_64')
license=('GPL3')
url='https://www.gnu.org/software/grep/'
groups=('base' 'base-devel')
depends=('glibc' 'pcre')
makedepends=('texinfo')
validpgpkeys=('155D3FC500C834486D1EEA677FD9FCCB000BEEEE') # Jim Meyering
source=("https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig}
        testsuite.patch)
md5sums=('feca7b3e7c7f4aab2b42ecbfc513b070'
         'SKIP'
         '0d3082d1d1fb77e7575c106fc4df1341')

prepare() {
  cd $pkgname-$pkgver
  
  # http://git.savannah.gnu.org/cgit/grep.git/patch/?id=c505b0bce73c712f1297bc5f424e0de5e4ec3137
  patch -p1 -i $srcdir/testsuite.patch

  autoreconf
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --without-included-regex
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR=$pkgdir install
}
