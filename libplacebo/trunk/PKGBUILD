# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=libplacebo
pkgver=0.6.0
pkgrel=1
pkgdesc='Reusable library for GPU-accelerated video/image rendering primitives'
url='https://github.com/haasn/libplacebo'
arch=('x86_64')
license=('LGPL2.1')
depends=('vulkan-icd-loader' 'glslang' 'lcms2' 'shaderc' 'libshaderc_shared.so')
makedepends=('meson' 'ninja' 'vulkan-headers')
provides=('libplacebo.so')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/haasn/libplacebo/archive/v${pkgver}.tar.gz)
sha512sums=('7a5bad9b68553e7d7a9e0d89867d651ec211b0d5478d9c5398e95f39956569829004755b0276d0d8cad6bd7c290016cff454d1449f58a477046dba6c638e59d6')

build() {
  cd ${pkgbase}-${pkgver}
  arch-meson build \
    -D tests=true \
    -D vulkan=enabled \
    -D glslang=enabled \
    -D shaderc=enabled \
    -D lcms=enabled
  ninja -C build
}

check() {
  cd ${pkgbase}-${pkgver}
  ninja -C build test
}

package() {
  cd ${pkgbase}-${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
